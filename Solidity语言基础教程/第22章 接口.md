# 第22章 接口（Interface）

## 基本概念

Solidity语言中关于接口（Interface）的定义如下：

+ 接口用于和其它合约进行交互；
+ 接口不能实现任何函数；
+ 接口无法继承其它合约，但可以继承其它接口；
+ 接口中的所有函数声明必须是外部的（external）；
+ 接口不能定义构造函数；
+ 接口不能定义状态变量；
+ 接口不能定义函数修改器。

## 接口定义

接口的示例如下：

```
pragma solidity ^0.8.13;

interface Token {
    enum TokenType { Fungible, NonFungible }
    struct Coin { string obverse; string reverse; }
    function transfer(address recipient, uint amount) external;
}
```

接口的关键字是 `interface`，上述接口示例中声明了一个枚举类型 `TokenType`、一个结构类型 `Coin` 和一个函数 `transfer()`。

接口中的函数都会隐式的标记为 `virtual`，意味着他们会被重写并不需要 `override` 关键字。但是不表示重写函数可以再次重写，仅仅当重写的函数标记为 `virtual` 才可以再次重写。

合约访问接口中类型和函数的方式如：`Token.TokenType`、 `Token.Coin` 或 `Token.transfer()`。

接口可以继承其他的接口，遵循与合约同样的继承规则。例如：

```
pragma solidity ^0.8.13;

interface ParentA {
    function test() external returns (uint256);
}

interface ParentB {
    function test() external returns (uint256);
}

interface SubInterface is ParentA, ParentB {
    // 必须重新定义 test 函数，以表示兼容父合约含义
    function test() external override(ParentA, ParentB) returns (uint256);
}
```

## 合约例子

下面是一个合约例子，用来演示接口的声明与用法。

```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

// 接口示例
// 合约Counter是接口的实现，合约MyContract
// 把两个合约都部署到链上，MyContract接收Counter合约的地址作为参数
contract Counter {
    uint public count;

    function increment() external {
        count += 1;
    }
}

interface ICounter {
    function count() external view returns(uint);
    function increment() external;
}

contract MyContract {
    function incrementCounter(address _counter) external {
        ICounter(_counter).increment();
    }

    function getCount(address _counter) external view returns(uint) {
        return ICounter(_counter).count();
    }
}
```

以上示例中，定义了接口 `ICounter`，合约 `Counter` 是该接口的实现，合约 `MyContract` 接收合约 `Counter` 的地址作为参数，调用接口函数实现。

## 合约执行

我们在Remix中编译、部署和运行这个合约例子。执行结果如下图：

![](D:\资料\我的\项目\IT培训项目\区块链\课程\Solidity语言基础教程\images\remix-interface-1.png)

## 课程小结

本课程我们介绍了接口，包括接口的基本概念与接口的声明、继承等。并且通过合约例子演示了接口的用法。


