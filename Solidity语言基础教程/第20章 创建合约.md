# 第20章 创建合约

## 基本概念

Solidity语言中关于合约的定义如下：

+ 合约类似于面向对象语言中的类；
+ 合约部署后得到一个地址（address），这个地址相当于合约的对象；
+ 可以通过new关键字来创建一个新的合约对象;
+ 可以从"外部"或从合约内部创建合约，外部创建如Remix集成环境或Web3.js接口等。

## 合约操作

在合约内部，有以下三种操作合约的方法：

```
// 方法一
ContractName c = new ContractName(arg1, arg2, ...);
// 方法二
ContractName c = ContractName(contract_address);
// 方法三
function functionName(ContractName c) public {
	// todo
}
```

其中：

方法一：通过 `new` 关键字直接创建一个新的合约，就像部署合约一样，会生产一个新的合约地址和合约对象；

方法二：根据合约地址引用已经new过的合约，可以使用被引用合约的方法和变量；

方法三：直接在执行合约方法时传入其它合约的地址以达到在本合约中调用其它合约方法的目的。

## 合约例子

下面是一个合约例子，用来演示创建合约的使用方法。

```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

// 合约创建与操作方法
contract Base {
    // Creator 为创建此合约的合约
    Creator public creator;
    string public name;

    // 构造函数，设置 creator 和 name
    constructor(string memory _name) {
        creator = Creator(msg.sender);
        name = _name;
    }

    // 更改 name，只有合约的创建者可以更改
    function changeName(string memory newName) public {
        if (msg.sender == address(creator))
            name = newName;
    }
}

// 合约创建者
contract Creator {
    // 声明被创建合约
    Base public base;

    // 创建合约，返回合约地址
    function createBase(string memory name) public {
        base = new Base(name);
    }
    
    // 引用已创建的合约
    function referBase(address baseAddress) public {
        base = Base(baseAddress);
    }

    // 更改被创建合约的 name
    function changeName(string memory name) public {
        base.changeName(name);
    }

    // 取被创建合约的 name
    function getName() public view returns(string memory) {
        return base.name();
    }
}
```

上面这个例子中，我们定义了两个合约，一个是 `Base` 合约，一个 `Creator` 合约。

合约 `Base` 为被调用者，定义了以下两个函数：

+ `constructor()`：合约的构造函数，用于初始化创建者合约的对象和 `name` 状态变量；
+ `changeName()`：更改 `name` 状态变量的函数，定义为只有本合约的创建者才能有更改权限。

合约 `Creator` 为创建者，声明了被创建者合约的状态变量，并定义了以下几个函数：

+ `createBase()`：通过 `new` 创建新合约，并返回一个合约对象（地址）；
+ `referBase()`：通过合约地址引用一个已创建的合约；
+ `changeName()`：通过调用被创建合约的方法更改被创建合约的 `name` 值；
+ `getName()`：通过调用被创建合约的方法获取被创建合约的 `name` 值，这是一个getter函数。

## 合约执行

我们在Remix中编译、部署和运行这个合约例子。

**1. 执行createBase()函数创建新合约，输出结果如下图：**

![](D:\资料\我的\项目\IT培训项目\区块链\课程\Solidity语言基础教程\images\remix-contract-1.png)

**2. 执行changeName()函数更新已创建合约的name，输出结果如下图：**

![](D:\资料\我的\项目\IT培训项目\区块链\课程\Solidity语言基础教程\images\remix-contract-2.png)

**3. 执行referBase()函数引用已创建合约，输出结果如下图：**

![](D:\资料\我的\项目\IT培训项目\区块链\课程\Solidity语言基础教程\images\remix-contract-3.png)

## 课程小结

本课程我们介绍了创建合约，包括创建合约的几种操作方法。并且通过合约例子演示了创建合约的用法。

