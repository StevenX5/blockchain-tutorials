# 第23章 库（Library）

## 基本概念

Solidity语言中关于库（Library）的定义如下：

+ 库和合约类似；
+ 库不能继承和被继承；
+ 库不能定义状态变量；
+ 库不能发送接收以太币；
+ 库不可以被销毁。

## 库的定义与调用

### 库的定义

定义库的示例如下：

```
library Math {
    function add(uint x, uint y) internal pure returns(uint) {
        uint z = x + y;
        require(z >= x, "uint overflow");
        return z;
    }
}
```

上面的示例定义了一个库，库的名称为 `Math`，在这个库中定义了一个库函数 `add()`。其中：

+ 库的关键字是 `library`，后面跟库的名称；
+ 库函数 `add()` 的可见性被设置成 `internal`，函数可见性也可以定义成 `public`。

**注：如果所有的库函数都是内部（internal）的，那么库的代码会嵌入到调用合约中，作为合约的上下文执行，这时无需事先部署该库；否则，需要先部署库，然后在部署调用合约之前先附加库函数。**

### 库的调用

合约在调用库之前，先附加库函数。语法如下：

```
using A for B;
```

该语句表示附加库函数（从库 `A`）到任何类型（`B`）作为成员函数，这些函数将接收到调用它们的对象作为它们的第一个参数。其中：

第一部分 `A` 可以是以下两种情形之一：

+ `using { f, g, h，L.t } for uint;`，指定库中的一些函数附加到类型，如：
+ `using L for uint;`，库中所有函数都被附加到类型，如：

第二部分 `B` 可以是以下两种情形之一：

+ 在文件级，`B` 必须是一个显式类型，如 `uint`；
+ 在合约内，`B` 可以是 `*`，如 `using L for *;`，表示库 `L` 中的函数被附加到所有类型上。

上述库示例的调用方式如下：

```
contract TestMath {
	using Math for uint;
	funttion testAdd(uint x, uint y) public pure returns(uint) {
		return x.add(y);
	}
}
```

## 合约例子

**例子1**：这个例子中，库函数的可见性为 `internal`，部署合约时无需事先部署库。

```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

// 数学运算的安全库
library SafeMath {
    function add(uint x, uint y) internal pure returns(uint) {
        uint z = x + y;
        require(z >= x, "uint overflow");
        return z;
    }
}

// 无需部署SafeMath库（因为库函数是internal）
contract TestSafeMath {
    using SafeMath for uint;

    function testAdd(uint x, uint y) public pure returns(uint) {
        return x.add(y);
    }
}
```

**输出**：我们在Remix中编译、部署和运行以上这个合约例子，执行结果如下图：

![](D:\资料\我的\项目\IT培训项目\区块链\课程\Solidity语言基础教程\images\remix-library-1.png)

**例子2**：这个例子中，库函数的可见性为 `public`，需要先部署库再部署合约。

```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

// 删除数组中元素的库
library Array {
    // 删除后将最后一个元素移到删除位置
    function remove(uint[] storage arr, uint idx) public {
        require(arr.length > 0, "cant remove from a empty array");
        arr[idx] = arr[arr.length - 1];
        arr.pop();
    }
}

// 需要先部署Array库（因为库函数是public）
contract TestArray {
    using Array for uint[];
    uint[] public arr;

    function testRemove() public {
        for (uint i = 0; i < 3; i++)
            arr.push(i);
        arr.remove(1);
        assert(arr.length == 2);
    }
}
```

**输出**：我们在Remix中编译、部署和运行以上这个合约例子，执行结果如下图：

![](D:\资料\我的\项目\IT培训项目\区块链\课程\Solidity语言基础教程\images\remix-library-2.png)

## 课程小结

本课程我们介绍了库，包括库的基本概念，库的定义与调用等。并且通过两个合约例子演示了库的用法。

