# 第16章 函数修改器

## 基本概念

Solidity语言中关于函数修改器的定义：

+ 函数修改器是在可以在函数调用之前或之后运行的代码；
+ 函数修改器可以被用于：
  + 限制访问
  + 验证输入
  + 防止重入攻击
+ 函数修改器是合约的可继承属性，可以被派生合约继承。

## 声明语法

函数修改器的声明语法如下：

```
modifier <function name>(paramslist) {
	block of code;
	_
	block of code;
}
```

其中：

+ **`modifier`**：函数修改器的关键字；
+ **`<function name>`**：修改器的函数名称；
+ **`paramslist`**：输入参数列表，可选；
+ **`_`**：下划线是函数修改器内部使用的特殊符号，用于替换函数体的代码。

***注意：如果一个函数有多个函数修改器，它们之间用空格隔开，函数修改器按顺序依次检查执行。***

## 合约例子

下面是一个合约例子，用来演示函数修改器的声明与用法。

```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

// 函数修改器
contract Modifier {
    // 状态变量
    address public owner;
    uint public x = 10;
    bool public locked;

    // 构造函数
    constructor() {
        owner = msg.sender;     // 当前合约所有者
    }

    // 修改器，用于检查调用方是否为所有者
    modifier onlyOwner() {
        require(msg.sender == owner, "Not owner");
        _;
    }

    // 修改器，用于检查传入的地址不是零地址
    modifier validAddress(address _addr) {
        require(_addr != address(0), "Not valid address");
        _;
    }

    // 改变owner的值
    function changeOwner(address _newOwner) public onlyOwner validAddress(_newOwner) {
        owner = _newOwner;
    }

    // 修改器，防止函数被重入
    modifier noReentrancy() {
        require(!locked, "Reentrancy");
        locked = true;
        _;
        locked = false;
    }

    // 这个函数受互斥保护，当函数运行时不能被重复调用
    function decrement(uint i) public noReentrancy {
        x -= i;
        if (i > 1) {
            decrement(i - 1);
        }
    }
}
```

在这个例子中，我们定义了3个函数修改器：

+ **`onlyOwner()`**

  这个函数修改器用于检查当前消息调用者（msg.sender）是否为合约的所有者，如果为true则继续执行函数的代码，否则返回“not owner”的提示。

+ **`validAddress()`**

  这个函数修改器用于检查传入的地址参数是否是一个零地址，如果不是零地址则继续执行函数的代码，否则返回“not valid address”的提示。

+ **`noReentrancy()`**

  这个函数修改器用于防止重入，即函数在运行时不能被重复调用。通过定义一个互斥锁来解决重入问题，当锁的状态为true时提示重入，相反当锁的状态为false时允许调用函数。

**输出** ：我们在Remix中编译、部署和运行这个样例合约，执行结果如下图：

![](D:\资料\我的\项目\IT培训项目\区块链\课程\Solidity语言基础教程\images\remix-modifier.png)

## 课程小结

本课程我们介绍了函数修改器，包括函数修改器的概念和声明语法。并且通过合约例子演示了函数修改器的声明与用法。

