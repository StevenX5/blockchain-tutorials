# 第21章 继承与重写

## 基本概念

Solidity语言中关于继承与重写的定义如下：

+ 合约支持多重继承包括多态，合约通过使用关键字 `is` 继承其它合约；
+ 被继承的合约称为基类合约（或称为父合约），继承合约称为派生合约（或称为子合约）；
+ 需要被子合约重写的函数必须声明为虚拟（virtual）函数；
+ 要覆盖父函数的函数必须使用重写（override）关键字；
+ 继承的顺序很重要，必须按照从"最基本的"到"最派生的"的顺序列出父合约。

## 声明语法

### 继承与重写

让我们看下面这个例子：

```
pragma solidity ^0.8.13;

contract A {
	function foo() external virtual {}
}

contract B is A {
	function foo() public override {}
}
```

上面的例子中，合约 `A` 是基类合约（父合约），函数 `foo()` 标记为 `virtual` 则允许继承合约重写。合约 `B` 继承于合约`A`，是合约 `A` 的派生合约，再合约 `B` 中重写函数 `foo()` 需要使用关键字 `override`，而且需要将可见性从 `external` 更改为 `public`。

### 多重继承

对于多重继承，如果有多个父合约且有相同的定义函数，则 `override` 关键字后面必须指定所有父合约名。

例如：

```
pragma solidity ^0.8.13;

contract Base1
{
    function foo() public virtual {}
}

contract Base2
{
    function foo() public virtual {}
}

contract Inherited is Base1, Base2
{
    function foo() public override(Base1, Base2) {}
}
```

上面的例子中，合约 `Inherited` 是一个多重继承合约，同时继承了合约 `Base1` 和合约 `Base2`，所以对于重写函数 `foo()`，除了需显式的指定 `override` 关键字外，还需要指定合约名。

### super关键字

在Solidity中，所有的函数调用都是虚拟的，这意味着最终派生的合约中的函数会被调用，除非明确给出合约名称或者使用`super` 关键字。当使用 `super` 调用函数时，调用的是继承合约（父合约）中的同名函数，不是合约自己的函数。例如下面这个例子：

```
pragma solidity ^0.8.13;

contract C {
	uint u;
	function f() public { u = 1; }
}

contract B is C {
	function f() public { u = 2; }
}

contract A is B {
	// will set u to 3
	function f() public { u = 3; }
	
	// will set u to 2
	function f1() { super.f(); }
    
	// will set u to 2
	function f2() { B.f(); }
	
	// will set u to 1
	function f3() { C.f(); }
}
```

### 修改器重写

修改器重写与函数重写类似，被重写的修改器也需要使用 `virtual` 修饰，重载也同样需要 `override` 修饰。

例如：

```
pragma solidity ^0.8.13;

contract Base
{
    modifier foo() virtual {_;}
}

contract Inherited is Base
{
    modifier foo() override {_;}
}
```

如果是多重继承，所有直接的父合约必须显示指定 `override`， 例如：

```
pragma solidity ^0.8.13;

contract Base1
{
    modifier foo() virtual { _;}
}

contract Base2
{
    modifier foo() virtual { _;}
}

contract Inherited is Base1, Base2
{
    modifier foo() override(Base1, Base2) { _;}
}
```

## 合约例子

下面是一个合约例子，用来演示继承与重写的用法。

```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

// 继承与重写
contract A {
    uint public u;
    function foo() public virtual { u = 1; }
}

// 合约B继承自合约A
contract B is A {
    // 重写A.foo()
    function foo() public virtual override { u = 2; }
}

// 合约C继承自合约A
contract C is A {
    // 重写A.foo()
    function foo() public virtual override { u = 3; }
}

// 合约继承自多个父合约
// 当在不同合约中多次定义的函数被调用时
// 父合约从右到左做被搜索（深度优先方式）
contract D is B, C {
    // u = 3
    // 因为C是函数foo()最右边的父合约
    function foo() public override(B, C) {
        return super.foo();
    }
}

contract E is C, B {
    // u = 2
    // 因为B是函数foo()最右边的父合约
    function foo() public override(C, B) {
        return super.foo();
    }
}

// 继承必须按"最基本的"到"最派生的"排序
// 交换A和B的顺序将引发编译错误
contract F is A, B {
    // u = 2
    function foo() public override(A, B) {
        return super.foo();
    }
}
```

## 合约执行

我们在Remix中编译、部署和运行这个合约例子。

合约D执行结果如下图：

![](D:\资料\我的\项目\IT培训项目\区块链\课程\Solidity语言基础教程\images\remix-inheritance-1.png)

合约E执行结果如下图：

![](D:\资料\我的\项目\IT培训项目\区块链\课程\Solidity语言基础教程\images\remix-inheritance-2.png)

合约F执行结果如下图：

![](D:\资料\我的\项目\IT培训项目\区块链\课程\Solidity语言基础教程\images\remix-inheritance-3.png)

## 课程小结

本课程我们介绍了继承与重写，包括继承、函数重写和修改器重写，以及`virtual`、`override`、`super`关键字的用法。并且通过合约例子演示了继承与重写的用法。
